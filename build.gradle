plugins {
	id 'css4j.java-conventions'
}

java {
	registerFeature('xmldtd') {
		usingSourceSet(sourceSets.main)
	}
	registerFeature('logging') {
		usingSourceSet(sourceSets.main)
	}
	withJavadocJar()
}

dependencies {
	api "io.sf.carte:carte-util:${carteutilVersion}"
	api "io.sf.carte:tokenproducer:${tokenproducerVersion}"
	api 'io.sf.jclf:jclf-text:[5.0.0,)'
	xmldtdImplementation "io.sf.carte:xml-dtd:${xmldtdVersion}"
	loggingImplementation 'org.slf4j:slf4j-api:[1.7.28,)'
	testImplementation "nu.validator:htmlparser:${htmlparserVersion}"
}

description = 'css4j'

tasks.register('testsJar', Jar) {
	archiveClassifier = 'tests'
	from(sourceSets.test.output)
}

configurations {
	tests
}

artifacts {
	tests testsJar
}

publishing.publications.maven {
	pom {
		description = "Core module"
	}
	artifact(testsJar)
}
